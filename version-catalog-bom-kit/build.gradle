plugins {
    id 'java'
    id 'java-library'
    id 'version-catalog'
    id 'maven-publish'
}

version = VERSION
group = PROJECT_GROUP

repositories {
    mavenCentral()
}

dependencies {


    implementation("org.apache.commons:commons-collections4:$LIB_APACHE_COMMONS_COLLECTIONS_VER")

}


catalog {
    versionCatalog {

        plugin("spring-dependency-management", "io.spring.dependency-management").version("$LIB_SPRING_DEP_MANAGEMENT")
        plugin("spring-boot", "org.springframework.boot").version("$LIB_SPRING_BOOT_VER")


        library("apache.poi", "org.apache.poi:poi:$LIB_APACHE_POI_VER")
        library("apache.poi-ooxml", "org.apache.poi:poi-ooxml:$LIB_APACHE_POI_VER")
        library("commons-lang3", "org.apache.commons:commons-lang3:$LIB_APACHE_COMMON_LANG_VER")
//        library("commons-collections4", "org.apache.commons:commons-collections4:$LIB_APACHE_COMMONS_COLLECTIONS_VER")
        library("commons-text", "org.apache.commons:commons-text:$LIB_APACHE_COMMONS_TEXT_COLLECTIONS_VER")
        library("commons-io", "commons-io:commons-io:$LIB_COMMONS_IO_VER")
    }
}


publishing {
    publications {
        maven(MavenPublication) {
            groupId = "${project.group}"
            artifactId = "${project.name}"
            version = "${project.version}"

            pom.withXml {
//                asNode().remove(asNode().get("dependencyManagement"))
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST

                    name 'Common Library BOM'
                    description 'Common Library BOM'

                    properties {

                        "options.encoding" "UTF-8"

                        project.rootProject.properties.each { it1 ->
                            if (it1.key.startsWith("LIB_") || it1.key == "JAVA_VERSION") {
                                "${it1.key}" "${it1.value}"
                            }
                        }
                    }

                    dependencyManagement {
                        dependencies {
                            project.configurations.each { conf ->
                                conf.dependencies.each { dep ->
                                    if (dep.version) {
                                        dependency {
                                            groupId "${dep.group}"
                                            artifactId "${dep.name}"
                                            version "${dep.version}"
                                        }
                                    }

                                }
                            }
                        }

                    }
                }
            }

            from(components["versionCatalog"])

        }
    }
}